managed implementation in class zbp_r_da2309_travel unique;
strict ( 2 );
with draft;

/* Wurzelknoten Travel */
define behavior for ZR_DA2309_Travel alias Travel
persistent table zda2309_atravel
draft table zda2309_dtravel
lock master
total etag LastChangedAt
authorization master ( instance )
//etag master Lastchangedat
{
  create;
  update;
  delete;
  association _Bookings { create; with draft; }

  draft action Edit;
  draft action Resume;
  draft action Activate optimized;
  draft action Discard;
  draft determine action Prepare {
   validation validate_dates;
   validation validate_customer;
   validation validate_agency;
  }

  static action show_message;
  action cancel_travel result [1] $self;
  action maintain_booking_fee parameter ZA_DA2309_BookingFee result [1] $self;

  validation validate_dates on save { create; }
  validation validate_customer on save { create; }
  validation validate_agency on save { create; }

  determination determine_status on modify { create; }
  determination determine_travel_id on modify { create; }

  field ( readonly, numbering : managed ) TravelUuid;
  field ( mandatory : create ) AgencyId, BeginDate, CustomerId, Description, EndDate;
  field ( readonly : update ) AgencyId, BeginDate, CustomerId, Description, EndDate;
  field ( readonly ) Createdat, Createdby, Lastchangedat, Lastchangedby, Status, TravelId;

  mapping for zda2309_atravel corresponding
    {
      AgencyId     = agency_id;
      BeginDate    = begin_date;
      BookingFee   = booking_fee;
      CurrencyCode = currency_code;
      CustomerId   = customer_id;
      EndDate      = end_date;
      TotalPrice   = total_price;
      TravelId     = travel_id;
      TravelUuid   = travel_uuid;
    }

}

/* Kindknoten Booking */
define behavior for ZR_DA2309_Booking alias Booking
persistent table zda2309_abooking
draft table zda2309_dbooking
lock dependent by _Travel
authorization dependent by _Travel
{
  update;
  delete;

  field ( readonly, numbering : managed ) BookingUuid;
  field ( readonly ) TravelUuid;

  association _Travel { with draft; }

  mapping for zda2309_abooking corresponding
    {
      BookingDate  = booking_date;
      BookingId    = booking_id;
      BookingUuid  = booking_uuid;
      CarrierId    = carrier_id;
      ConnectionId = connection_id;
      CurrencyCode = currency_code;
      CustomerId   = customer_id;
      FlightDate   = flight_date;
      FlightPrice  = flight_price;
      TravelUuid   = travel_uuid;
    }

}